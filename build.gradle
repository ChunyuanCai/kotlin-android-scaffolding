apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'

buildscript {
  ext.kotlin_version = '1.0.6'
  ext.gradle_tools = '2.2.3'
  ext.build_tools = '25.0.2'

  ext.compile_sdk = 25
  ext.target_sdk = 25
  ext.min_sdk = 19

  ext.application_id = 'com.fernandocejas.android'
  ext.version_name = '1.0'
  ext.version_code = 1
  ext.test_runner = 'android.support.test.runner.AndroidJUnitRunner'

  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$gradle_tools"
    //noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

task runUnitTests(dependsOn: [':app:testDebugUnitTest']) {
  description 'Run all unit tests'
}

task runAcceptanceTests(dependsOn: [':app:connectedAndroidTest']) {
  description 'Run all acceptance tests.'
}

task deployDebug(type: Exec, dependsOn: 'app:installDebug') {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, "local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream {
      inputStream -> properties.load(inputStream)
    }
    def sdkDir = properties.getProperty('sdk.dir')
    def adb = "$sdkDir/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.fernandocejas.android/com.fernandocejas.android.MainActivity'
  }
}
